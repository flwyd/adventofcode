#!/bin/zsh
# Copyright 2025 Trevor Stone
#
# Use of this source code is governed by an MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT.

# Run a Jsonnet Advent of Code program.  This script runs importstr as part of a
# top-level argument rather than using --tla-str-file because the top level
# argument is a list of file objects, runner doesn't know how many files.
# It also sets the verbose TLA if -v is passed to this script.
#
# TODO Find a way for jsonnet to exit non-zero if expected not matched, but
# still output values to stdout (making assertEquals challenging).

basedir=${0:h}
args=()
if [[ $1 == -v ]]; then
  args+=(--tla-code verbose=true)
  shift
fi

if (( $# == 0 )); then
  print -u 2 "Usage: $0 -v day1/day1.jsonnet day1/input.example.txt ..."
  exit 1
fi
program=$1
shift

if (( $# == 0 )); then
  args+=(--tla-code 'files=[{name:"(stdin)",content:importstr "/dev/stdin"}]')
else
  files=""
  foreach infile ($@) do
    expectedfile=${infile:r}.expected
    expected=""
    if [ -e $expectedfile ]; then
      expected="expected:importstr '$expectedfile'"
    fi
    files+="{name:'$infile', content:importstr '$infile', $expected},"
  done
  args+=(--tla-code "files=[$files]")
fi
set -x
jsonnet -t 64 -s 3000 -S $basedir/runner.jsonnet --tla-code-file "solve=$program" $args
